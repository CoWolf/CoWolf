package de.uni_stuttgart.iste.cowolf.model.lqn.analysis

import de.uni_stuttgart.iste.cowolf.model.LqnCore.ActivityMakingCallType
import de.uni_stuttgart.iste.cowolf.model.LqnCore.ActivityType
import de.uni_stuttgart.iste.cowolf.model.LqnCore.DocumentRoot
import de.uni_stuttgart.iste.cowolf.model.LqnCore.EntryType
import de.uni_stuttgart.iste.cowolf.model.LqnCore.PhaseActivities
import de.uni_stuttgart.iste.cowolf.model.LqnCore.ProcessorType
import de.uni_stuttgart.iste.cowolf.model.LqnCore.SolverParamsType
import de.uni_stuttgart.iste.cowolf.model.LqnCore.TaskType
import java.math.BigInteger
import java.util.Map
import java.util.HashMap

class LQNSolverFileStandardGenerator implements LQNSolverFileGenerator {
	
	private val processorSchedules = #{'fcfs'->'f'};
	private val taskSchedules = #{'ref'->'r', 'fcfs'->'f'};
	private val nophases = 3;
	
	private Map<String,String> entries;
	
	
	override doGenerateLQNSolverFile(DocumentRoot root, Map<String, Object> parameters) {
		init(root);
		return '''«lqnStandardTemplate(root, parameters)»'''
	}
	
	def init(DocumentRoot root){
		entries = new HashMap<String,String>();
		for(EntryType e : root.eAllContents.filter(EntryType).toIterable){
			if(e.name != null && e.id != null){
				entries.put(e.name,e.id);
			}
		}
	}
	
	def lqnStandardTemplate(DocumentRoot r, Map<String, Object> parameters)'''
		«processSolverParams(r.lqnModel.solverParams)»
		
		P 0
		«FOR p : r.lqnModel.processor»
		«p.processProcessor»
		«ENDFOR»
		-1
		
		T 0
		«FOR t : r.lqnModel.eAllContents.filter(TaskType).toList»
		«t.processTask»
		«ENDFOR»		
		-1
		
		E 0
		«FOR e : r.lqnModel.eAllContents.filter(EntryType).toList»
		«e.processEntry»
		«ENDFOR»
		-1
		
		«FOR a : r.lqnModel.eAllContents.filter(ActivityType).toList»
		«a.processActivities»
		«ENDFOR»
	'''
	
	def processSolverParams(SolverParamsType solverParams)
	'''G "LQN Model. Generated by CoWolf"
«solverParams.convVal»
«solverParams.itLimit»
«solverParams.printInt»
«solverParams.underrelaxCoeff»
-1'''
	
	def processProcessor(ProcessorType processor)'''
		p «processor.id.beatifyString» «processorSchedules.get(processor.scheduling.getName())» «processor.multiplicity»
	'''
	
	def processTask(TaskType task)'''
		t «task.id.beatifyString» «taskSchedules.get(task.scheduling.getName())» «FOR e : task.entry SEPARATOR ' '»«e.id.beatifyString»«ENDFOR» -1 «ProcessorType.cast(task.eContainer).id.beatifyString»
	'''
	
	def processEntry(EntryType entry)'''
		«IF entry.entryActivityGraph != null»
		A «entry.id.beatifyString» «entry.entryActivityGraph.activity.iterator.next.id.beatifyString»
		«ENDIF»
		«IF entry.forwarding != null && entry.forwarding.iterator.hasNext»
		F «entry.id.beatifyString» «entry.forwarding.iterator.next.id.beatifyString»
		«ENDIF»
		«IF entry.entryPhaseActivities != null»
		s «entry.id.beatifyString» «processPhaseActivities(entry.entryPhaseActivities)» -1
		«FOR a : entry.entryPhaseActivities.activity»
		«FOR c : a.synchCall»
		y «entry.id.beatifyString» «entries.get(c.dest).beatifyString» «processActivityMakingCallType(c,a.phase)»-1
		«ENDFOR»
		«ENDFOR»
		«ENDIF»		
	'''
	
	def processActivities(ActivityType activity)'''		
	
	'''
	
	def processPhaseActivities(PhaseActivities pActivities){
		var str = "";
		for(var i=1; i<=nophases; i++){
			var found = false;
			for(var j=0; j<pActivities.activity.length; j++){
				var a = pActivities.activity.get(j);
				if (a.phase.intValue == i && a.hostDemandMean != null){
					str += ' '+a.hostDemandMean+' ';
					found = true;
				}
			}
			if(!found){
				str += ' 0 ';
			}
		}
		return str;
	}
	
	def processActivityMakingCallType(ActivityMakingCallType callType, BigInteger activityPhase){
		var str = "";
		for(var i=1; i<=nophases; i++){
			if (activityPhase.intValue == i && callType.callsMean != null){
				str += ' '+callType.callsMean+' ';
			}
			else{
				str += ' 0 ';
			}
		}
		return str;
	}
	
	def beatifyString(String str){
		return str.replaceAll("-","__");
	}
}