grammar de.uni_stuttgart.iste.cowolf.model.ctmc.xtext.PCTL with org.eclipse.xtext.common.Terminals hidden(WS)
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate pCTL "http://www.uni_stuttgart.de/iste/cowolf/model/ctmc/xtext/PCTL"

Start:
	{Start}rule+=Fragment*
;

terminal BOOL : 'true'|'false';

terminal DOUBLE returns ecore::EDouble : INT ('.' INT)? | ('.' INT);

terminal COMPARATOR returns ecore::EString : '>''='?|'<''='?;

Fragment returns Fragment:
    Comment | Rule
;

Comment:
    comment=SL_COMMENT
;

Rule hidden(WS):
    (BooleanRule|SteadyState|QuantifiedProbability)
;

SteadyState:
    "S" SL_COMMENT* (bound=CompareProbability|"=?") SL_COMMENT* "[" SL_COMMENT* value=StateFormula SL_COMMENT* "]";
    

QuantifiedProbability:
    "P" SL_COMMENT* "=?" SL_COMMENT* "[" value=PathFormula "]"
;

BooleanRule:
    value=StateFormula
;


//###########################################################
//#             State Formular expressions                  #
//###########################################################

StateFormula:
    '(' SL_COMMENT* left=StateFormula (right=Junction|right=Implication)? SL_COMMENT* ')' | left=(Negation| Atomic| Probability)
;

Negation returns StateExpression:
    "!" StateFormula
;

Junction returns RightStateExpression hidden(WS,SL_COMMENT):
    Conjunction
    | Disjunction
;

Conjunction returns RightStateExpression:
    '&' StateFormula ({Conjunction.left=current} '&' right=StateFormula)*
;

Disjunction returns RightStateExpression:
    '|' StateFormula ({Disjunction.left=current} '|' right=StateFormula)*
;

Implication returns RightStateExpression hidden(WS,SL_COMMENT):
    "=>" right=StateFormula
;  

Probability returns StateExpression:
    "P" SL_COMMENT* {Probability} comparator=CompareProbability SL_COMMENT* "[" path=PathFormula "]"
;   
    

Atomic returns StateExpression:
     (State|Label|Boolean)
;
    
State:
    'State:' value=STRING;

Label:
    'Label:' value=STRING;
    
Boolean:
    value=BOOL;
    
//###########################################################
//#             Path Formular expressions                  #
//###########################################################

PathFormula hidden(WS,SL_COMMENT):
      Next
    | Until
    | Future
    | Globally
;
	
Until:
    left=StateFormula type=('U' | 'W' | 'R') bound=TimeBound? right=StateFormula
;

Next:
    "X" bound=TimeBound? value=StateFormula;

Future:
    'F' bound=TimeBound? value=StateFormula
;

Globally:
    'G' bound=TimeBound? value=StateFormula
; 
	
CompareProbability hidden(WS,SL_COMMENT):
    operator=COMPARATOR value=DOUBLE
;

TimeBound hidden(WS,SL_COMMENT):
	(operator=(COMPARATOR|'=') value=DOUBLE) | ("[" from=DOUBLE operator="," to=DOUBLE "]");